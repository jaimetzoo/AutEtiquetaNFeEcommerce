--CRIANDO A TABELA NO INTEGRA QUE VAI DEFINIR AS UNIDADES QUE SERAM HABILITADAS PARA EMISSAO AUTOMATICA
/*
DROP TABLE INTEGRA.UNIDADE_NFE_AUT_ECOM;
CREATE TABLE INTEGRA.UNIDADE_NFE_AUT_ECOM (
    UNIDADE             NUMBER(3)   NOT NULL,
    NFE_AUT_ECOM        CHAR(1)     DEFAULT 'N'     CHECK (NFE_AUT_ECOM IN ('S','N')),
    NFE_AUT_RETIRA      CHAR(1)     DEFAULT 'N'     CHECK (NFE_AUT_RETIRA IN ('S','N')),
    NFE_AUT_ENTREGA     CHAR(1)     DEFAULT 'N'     CHECK (NFE_AUT_ENTREGA IN ('S','N')),
    NFE_AUT_EXPRESSA    CHAR(1)     DEFAULT 'N'     CHECK (NFE_AUT_EXPRESSA IN ('S','N')),
    ETQ_AUT_VOLUME      CHAR(1)     DEFAULT 'N'     CHECK (ETQ_AUT_VOLUME IN ('S','N')),
    INCLUSAO            DATE        DEFAULT SYSDATE,
    CONSTRAINT PK_UNIDADES_NFE_AUT_ECOM PRIMARY KEY (UNIDADE)
);
*/

--INSERINDO A UNIDADE
/*
INSERT INTO INTEGRA.UNIDADE_NFE_AUT_ECOM (UNIDADE) VALUES (202);
*/

--HABILITANDO A UNIDADE PARA EMISSAO AUTOMATICA
/*
UPDATE INTEGRA.UNIDADE_NFE_AUT_ECOM SET NFE_AUT_ECOM = 'S'
WHERE UNIDADE = 209;
*/


--CRIANDO A TRIGGER
CREATE OR REPLACE TRIGGER INTEGRA.TR_T119_EMISSAO_AUTO_ECOM
AFTER UPDATE OF T119_SITUACAO_PRENOTA OR INSERT ON DBAMDATA.T119_PRENOTA REFERENCING NEW AS NEW OLD AS OLD
FOR EACH ROW
DECLARE
/******************************************************************************
	NOME:        : TR_T119_EMISSAO_AUTO_ECOM
	OBJETIVO     : Emiss�o autom�tica de NFe para prenotas de e-commerce 
	DESCRICAO    : Inserir a prenota de origem 4-ecommerce na tabela TPRE_EMISSAO_NFE_AUTO para emiss�o 
				   autom�tica de NFe quando a situa��o da prenota for alterada para 3-Confer�ncia Conclu�da,
				   do tipo RETIRA/ENTREGA e apenas quando os par�metros no mdlog estiverm desabilitados.
	DATA CRIA��O : 29/09/2025
	PROGRAMADOR  : Jaime Pereira
******************************************************************************/
    vNFE_AUT_RETIRA      			INTEGRA.UNIDADE_NFE_AUT_ECOM.NFE_AUT_RETIRA%TYPE;
    vNFE_AUT_ENTREGA                INTEGRA.UNIDADE_NFE_AUT_ECOM.NFE_AUT_ENTREGA%TYPE;
	vT908_SOLIC_ATZ_AUTO_NFE_PRENT  T908_PARAM_EMPRESA.T908_SOLIC_ATZ_AUTO_NFE_PRENT%TYPE;
	vT908_CHECK_CLIENTE_RETIRA      T908_PARAM_EMPRESA.T908_CHECK_CLIENTE_RETIRA%TYPE;
	vT908_CHECK_ENTREGA             T908_PARAM_EMPRESA.T908_CHECK_ENTREGA%TYPE;
	vCOUNT_UNID_ECOM				NUMBER(1) := 0;

	PROCEDURE GRAVA_PRENOTA_EMISSAO_AUTO (PI_UNIDADE IN NUMBER, PI_PRENOTA IN VARCHAR2)
	IS
	BEGIN
		BEGIN
		  INSERT INTO TPRE_EMISSAO_NFE_AUTO
					( TPRE_UNIDADE,
					  TPRE_NUMERO_PRENOTA)
			 VALUES ( PI_UNIDADE,
					  PI_PRENOTA );
		EXCEPTION WHEN OTHERS THEN
			NULL;
		END;
	END;


BEGIN
    -- APENAS PRENOTAS DE ORIGEM 4-ECOMMERCE E TIPO NOTA FISCAL A-VENDA
    IF (:NEW.T119_ORIGEM_PRENOTA = 4 AND :NEW.T119_TIPO_NOTAFISC = 'A') THEN

		--BUSCANDO A UNIDADE DA PRENOTA 
		BEGIN
			SELECT COUNT(*), MAX(NFE_AUT_RETIRA), MAX(NFE_AUT_ENTREGA)
			INTO vCOUNT_UNID_ECOM, vNFE_AUT_RETIRA, vNFE_AUT_ENTREGA
			FROM INTEGRA.UNIDADE_NFE_AUT_ECOM
			WHERE UNIDADE = NVL(:NEW.T119_UNIDADE_LOGISTICA_E,:NEW.T119_UNIDADE_IE)
			AND NFE_AUT_ECOM = 'S'
            AND (NFE_AUT_RETIRA = 'S' OR NFE_AUT_ENTREGA = 'S');
		EXCEPTION WHEN OTHERS THEN
			vCOUNT_UNID_ECOM := 0;
		END;

		--APENAS UNIDADES HABILITADAS PARA EMISSAO AUTOMATICA
		IF (vCOUNT_UNID_ECOM > 0) THEN
		
			-- SE SITUA��O PRENOTA MUDAR(UPDATE OU INSERT)
			IF (NVL(:NEW.T119_SITUACAO_PRENOTA,'1') <> NVL(:OLD.T119_SITUACAO_PRENOTA,'1')) THEN
		
				-- S� PARA PRENOTAS COM SITUACAO 3-CONFERENCIA CONCLUIDA E DIFERENTE DA SITUACAO ANTERIOR P-AQUARDANDO SEFAZ
				IF (:NEW.T119_SITUACAO_PRENOTA = '3' AND NVL(:OLD.T119_SITUACAO_PRENOTA,'1') <> 'P') THEN
				
					BEGIN
					  SELECT T908_SOLIC_ATZ_AUTO_NFE_PRENT,
							T908_CHECK_CLIENTE_RETIRA,
							T908_CHECK_ENTREGA
						INTO vT908_SOLIC_ATZ_AUTO_NFE_PRENT,
							vT908_CHECK_CLIENTE_RETIRA,
							vT908_CHECK_ENTREGA
						FROM T908_PARAM_EMPRESA
					  WHERE T908_UNIDADE_IE = NVL(:NEW.T119_UNIDADE_LOGISTICA_E,:NEW.T119_UNIDADE_IE);
					EXCEPTION WHEN NO_DATA_FOUND THEN
					  vT908_SOLIC_ATZ_AUTO_NFE_PRENT := '1';
					END;
		
					--SE PARAMETRO MDLOG ESTIVER HABILITADO PARA TODOS, VERIFICA TIPO DE ENTREGA E SE CLIENTE RETIRA/ENTREGA TA DESABILITADO
					IF (vT908_SOLIC_ATZ_AUTO_NFE_PRENT = '2') THEN
                    
                        --"0" - Cliente Retira ou "1" - Entrega
						IF (:NEW.T119_TIPO_ENTREGA = '0') AND (vNFE_AUT_RETIRA = 'S') AND (vT908_CHECK_CLIENTE_RETIRA = 'N') THEN
						  GRAVA_PRENOTA_EMISSAO_AUTO (:NEW.T119_UNIDADE_IE, :NEW.T119_NUMERO_PRENOTA_IU);
						ELSIF (:NEW.T119_TIPO_ENTREGA = '1') AND (vNFE_AUT_ENTREGA = 'S') AND (vT908_CHECK_ENTREGA = 'N') THEN
						  GRAVA_PRENOTA_EMISSAO_AUTO (:NEW.T119_UNIDADE_IE, :NEW.T119_NUMERO_PRENOTA_IU);
						END IF;
						
					END IF;
		
				END IF;
		
			END IF;
		
		END IF;
    
    END IF;

 EXCEPTION
  WHEN OTHERS THEN
    NULL;
END TR_T119_EMISSAO_AUTO_ECOM;

